
'Question 1:
Function extractColumn(Matrix() As Variant, colIndex As Integer) As Variant()

Dim n As Integer        ' Nombre de lignes dans la matrice
Dim i As Integer        ' Variable de boucle
Dim Col() As Variant    ' Tableau pour stocker la colonne extraite
    

n = UBound(Matrix, 1)
    

ReDim Col(1 To n)
    
' Boucle à travers les lignes de la matrice pour extraire la colonne spécifiée
For i = 1 To n
    Col(i) = Matrix(i, colIndex)
Next i
    
' Renvoie la colonne extraite
extractColumn = Col()
End Function

'Question 2 :
Function computeSignal(Ri() As Variant) As Variant

Dim s As Variant    ' Variable pour stocker la somme des éléments du vecteur
    
' Calcul de la somme des éléments du vecteur d'entrée
s = WorksheetFunction.Sum(Ri)
    
' Renvoi du résultat (la somme)
computeSignal = s
End Function

'Question 3:
Function momSignalVector(data() As Variant) As Variant()

Dim n As Integer           ' Nombre de colonnes dans la matrice de données
Dim signal() As Variant     ' Vecteur de signaux
Dim Ri() As Variant         ' Vecteur temporaire pour stocker une colonne de données
Dim i As Integer           ' Variable de boucle

n = UBound(data, 2)

ReDim signal(1 To n)

' Boucle à travers chaque colonne de la matrice de données pour calculer le signal de chaque actionet l'intégrer au vecteur Ri()
For i = 1 To n
    Ri = extractColumn(data, i)
    signal(i) = computeSignal(Ri)
      
Next i
' Renvoi du vecteur de signaux calculé
momSignalVector = signal

End Function
'Question 4:
Function createIndex(vector() As Variant) As Variant()

Dim n As Integer           ' Nombre d'éléments dans le vecteur
Dim vect() As Variant       ' Index à deux colonnes
Dim i As Integer           ' Variable de boucle

n = UBound(vector, 1)

ReDim vect(1 To n, 1 To 2)

' Boucle à travers chaque élément du vecteur
For i = 1 To n
    vect(i, 1) = i
    vect(i, 2) = vector(i)
Next i

' Renvoi de l'index à deux colonnes
createIndex = vect

End Function

'Question 5:
Function assignToPF(Matrix() As Variant) As Variant()

Dim n As Integer           ' Nombre d'actions dans la matrice
Dim vect() As Variant       ' Vecteur à trois colonnes pour attribuer les actions aux portefeuilles
Dim i As Integer           ' Variable de boucle pour les portefeuilles
Dim j As Integer           ' Variable de boucle pour les actions dans chaque portefeuille

n = UBound(Matrix, 1)

ReDim vect(1 To n, 1 To 3)

' Boucle pour attribuer chaque action à l'un des cinq portefeuilles
For i = 1 To 5
    For j = 1 To 20
        ' Attribution de l'indice de l'action à la première colonne du vecteur
        vect((i - 1) * 20 + j, 1) = Matrix((i - 1) * 20 + j, 1)
        ' Attribution de la valeur de l'action à la deuxième colonne du vecteur
        vect((i - 1) * 20 + j, 2) = Matrix((i - 1) * 20 + j, 2)
        ' Attribution du numéro de portefeuille à la troisième colonne du vecteur
        vect((i - 1) * 20 + j, 3) = i
    Next j
Next i

' Renvoi du vecteur attribuant les actions aux portefeuilles
assignToPF = vect

End Function

'Question 6:
Function weightsVector(Matrix() As Variant, PFnumber As Integer) As Variant()

Dim n As Integer           ' Nombre d'actions dans la matrice
Dim weights() As Variant    ' Vecteur de poids pour le portefeuille spécifique
Dim i As Integer           ' Variable de boucle

n = UBound(Matrix, 1)

ReDim weights(1 To n)

' Boucle pour attribuer les poids aux actions du portefeuille spécifique
For i = 1 To n
    ' Vérification si l'action appartient au portefeuille spécifié (PFnumber)
    If Matrix(i, 3) = PFnumber Then
        ' Attribution du poids à l'action correspondante dans le vecteur de poids ( poids de 0.05 pour chaque action appartenant au portefeuille spécifié)
        weights(Matrix(i, 1)) = 0.05
    End If
Next i

' Renvoi du vecteur de poids pour le portefeuille spécifique
weightsVector = weights

End Function

'Question 7:
Function portfolioReturn(returnsVector() As Variant, weights() As Variant) As Variant

Dim n As Integer           ' Longueur du vecteur de rendements
Dim M As Integer           ' Longueur du vecteur de poids
Dim Rpf As Variant          ' Rendement du portefeuille

n = UBound(returnsVector)

M = UBound(weights)

' Calcul du rendement du portefeuille en utilisant le produit scalaire entre les vecteurs de rendements et de poids
Rpf = WorksheetFunction.SumProduct(returnsVector, weights)

' Renvoi du rendement du portefeuille
portfolioReturn = Rpf

End Function


'Question 8:
Sub MomentumStratImplementation()

Dep = Time


' Feuilles de calcul
Dim wsData, wsResults As Worksheet            ' Feuille de donnéeset de résultats


' Variables pour le traitement des données
Dim nbDate, nbStocks As Integer              ' Nombre de lignes de données  et Nombre de colonnes d'actions

Dim data(), dataT() As Variant               ' Tableau pour stocker les données de la feuille de calcul  et Tableau temporaire pour stocker les données sur les 11 mois précédents
Dim i, j As Integer                     ' Variables pour la boucle
            
' Variables pour les calculs
Dim t As Integer                   ' Variable pour la boucle principale
Dim returns() As Variant            ' Tableau pour stocker les rendements
Dim mounths, stocks As Integer             ' Variable pour la boucle sur les mois et les actions
Dim signals(), index() As Variant            ' Tableau pour stocker les signaux et Tableau pour stocker l'index
Dim indexR As Range                ' Plage pour l'index trié
Dim indexS(), portfVector() As Variant             ' Tableau pour stocker l'index trié et Vecteur pour le portefeuille
Dim PF As Integer                  ' Variable pour la boucle sur les portefeuilles
Dim weights(), Results() As Variant            ' Tableau pour stocker les poids et Tableau pour stocker les résultats
Dim cumResults(), LSResults(), cumLSResults() As Variant         ' Tableau pour stocker les résultats cumulatifs,Tableau pour stocker les rendements long-short et Tableau pour stocker les rendements long-short cumulatifs


' Définition des feuilles de calcul de données et de résultats
Set wsData = Worksheets(1)
Set wsResults = Worksheets(2)

' Détermination du nombre de lignes de données et de colonnes d'actions
nbDate = wsData.Cells(wsData.Rows.Count, 1).End(xlUp).Row - 1
nbStocks = wsData.Cells(1, 1).End(xlToRight).Column - 1 '


' Transfert des données de la feuille de calcul vers un tableau en mémoire
ReDim data(1 To nbDate, 1 To nbStocks)
For i = 1 To nbDate
    For j = 1 To nbStocks
        data(i, j) = wsData.Cells(i + 1, j + 1)
    Next j
Next i

' Initialisation des tableaux pour stocker les résultats
ReDim Results(1 To nbDate - 12, 1 To 5)
ReDim cumResults(1 To nbDate - 12, 1 To 5)
ReDim LSResults(1 To nbDate - 12)
ReDim cumLSResults(1 To nbDate - 12)

' Boucle principale pour chaque intervalle de 12 mois
For t = 1 To nbDate - 12

    ' Extraction des rendements pour l'intervalle de 12 mois
    ReDim returns(1 To nbStocks)
    For stocks = 1 To nbStocks
        returns(stocks) = data(t + 12, stocks)
    Next stocks
    
    ' Création d'une matrice temporaire pour les 11 mois précédents
    ReDim dataT(1 To 11, 1 To nbStocks)
    For mounths = 1 To 11
        For stocks = 1 To nbStocks
            dataT(mounths, stocks) = data(t + mounths - 1, stocks)
        Next stocks
    Next mounths
    
    ' Génération des signaux et création d'un index
    signals = momSignalVector(dataT)
    index = createIndex(signals)
    
    'Tri par ordre croissant du vecteur des signaux
    indexS = WorksheetFunction.Sort(index, 2)
    
    ' Attribution des actions à chaque portefeuille
    portfVector = assignToPF(indexS)
    
    ' Calcul des rendements pour chaque portefeuille
    For PF = 1 To 5
        weights = weightsVector(portfVector, PF)
        Results(t, PF) = portfolioReturn(returns, weights)
        If t = 1 Then
            cumResults(t, PF) = portfolioReturn(returns, weights)
        Else
            cumResults(t, PF) = portfolioReturn(returns, weights) + cumResults(t - 1, PF)
        End If
    Next PF
    
    ' Calcul des rendements long-short
    LSResults(t) = Results(t, 5) - Results(t, 1)
    cumLSResults(t) = cumResults(t, 5) - cumResults(t, 1)
    
Next t


' Stockage des résultats dans la feuille de calcul des résultats
wsResults.Cells(2, 2).Resize(nbDate - 12, 5).Value = Results
wsResults.Cells(2, 8).Resize(nbDate - 12, 5).Value = cumResults
wsResults.Cells(2, 14).Resize(nbDate - 12, 1).Value = WorksheetFunction.Transpose(LSResults)
wsResults.Cells(2, 15).Resize(nbDate - 12, 1).Value = WorksheetFunction.Transpose(cumLSResults)

MsgBox ((Time - Dep) * 3600 * 24)
End Sub
